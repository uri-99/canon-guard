// SPDX-License-Identifier: MIT
pragma solidity >=0.8.30;

import {IGuardManager} from '@safe-smart-account/interfaces/IGuardManager.sol';
import {ActionsBuilder} from 'contracts/actions-builders/ActionsBuilder.sol';
import {ICanonGuard} from 'interfaces/ICanonGuard.sol';
import {IActionsBuilder} from 'interfaces/actions-builders/IActionsBuilder.sol';
import {IChangeSafeGuardAction} from 'interfaces/actions-builders/IChangeSafeGuardAction.sol';

/**
 * @title ChangeSafeGuardAction
 * @notice Contract that builds an action to change the Safe guard
 * @dev Builds an action that calls SAFE with IGuardManager.setGuard and the new safe guard contract address
 */
contract ChangeSafeGuardAction is IChangeSafeGuardAction, ActionsBuilder {
  /// @inheritdoc IChangeSafeGuardAction
  address public immutable SAFE_GUARD;

  /**
   * @notice Constructor that sets up the ChangeSafeGuardAction contract
   * @param _safeGuard The new safe guard contract address. If the idea is to remove the guard, set it to address(0)
   */
  constructor(address _safeGuard) ActionsBuilder(msg.sender) {
    SAFE_GUARD = _safeGuard;
  }

  // ~~~ ACTIONS METHODS ~~~

  /// @inheritdoc ActionsBuilder
  function getActions() external view override(ActionsBuilder, IActionsBuilder) returns (Action[] memory _actions) {
    _actions = new Action[](1);
    _actions[0] = Action({
      target: address(ICanonGuard(msg.sender).SAFE()),
      data: abi.encodeCall(IGuardManager.setGuard, (SAFE_GUARD)),
      value: 0
    });
  }
}
